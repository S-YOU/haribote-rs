alpha    = _{ 'a' .. 'z' | 'A' .. 'Z' }
comma    = _{ "," }
dquate   = _{ "\"" }
dollar   =  { "$" }
hyphen   = _{ "-" }
underbar = _{ "_" }
space    = _{ " " }
colon    = _{ ":" }
lbranket = _{ "[" }
rbranket = _{ "]" }
ws       = _{ " " | "\r" | "\n" | "\t" }
empty    =  { ws* }

str     =  { (alpha | space | underbar | comma | hyphen | digit)* }
string  = _{ dquate ~ str ~ dquate } 

digit = _{ '0'..'9' }
num   = _{ '1'..'9' ~ digit* | "0" }

//caution! overflow
dec8  = { num } 
dec16 = { num }
dec32 = { num }

hex   = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }

hex8  = { "0x" ~ hex{2} }
hex16 = { "0x" ~ hex{4} }
hex32 = { "0x" ~ hex{8} }

imm8  = { hex8 | dec8 } 
imm16 = { hex16 | dec16 }
imm32 = { hex32 | dec32 }

//DB, DW, DD, RESB instructions
db      =  { "DB" }
db_exp  = _{ db ~ ws* ~ imm8 ~ ws* ~ (comma ~ ws* ~ imm8)*
           | db ~ ws* ~ string } 

dw      =  { "DW" }
dw_exp  = _{ dw ~ ws* ~ imm16 }

dd      =  { "DD" }
dd_exp  = _{ dd ~ ws* ~ imm32 }

resb     =  { "RESB" }
resb_exp = _{ resb ~ ws* ~ (imm16 ~ ws* ~ hyphen ~ ws* ~ dollar | imm16 )}

//16-bit register 
//AX CX DX BX SP BP SI DI
//AL CL DL BL AH CH DH BH
//ES, CS, SS, DS, FS, GS
//32-bit 
//EAX, ECX, EDX, EBX, ESP, EBP, ESI, EDI
ax = { "AX" } cx = { "CX" } dx = { "DX" } bx = { "BX" }
sp = { "SP" } bp = { "BP" } si = { "SI" } di = { "DI" }
al = { "AL" } cl = { "CL" } dl = { "DL" } bl = { "BL" }
ah = { "AH" } ch = { "CH" } dh = { "DH" } bh = { "BH" }
es = { "ES" } cs = { "CS" } ss = { "SS" } ds = { "DS" }
fs = { "FS" } gs = { "GS" }
eax = { "EAX" } ecx = { "ECX" } edx = { "EDX" } ebx = { "EBX" } esp = { "ESP" }
ebp = { "EBP" } esi = { "ESI" } edi = { "EDI" }
reg8  = _{ al | cl | dl | bl | ah | ch | dh | bh }
reg16 = _{ ax | cx | dx | bx | sp | bp | si | di }
reg32 = _{ eax | ecx | edx | ebx | esp | ebp | esi | edi }


//ORG     0x7c00  
org = { "ORG" }
org_exp  = _{ org ~ ws* ~ imm16 }

//JMP label
//:label
jmp = { "JMP" }
label = { alpha+ }
jmp_exp = _{ jmp ~ ws* ~ label }

je = { "JE" }
je_exp = _{ je ~ ws* ~ label }

dist_label = _{ label ~ colon }




add = { "ADD" }
add_exp = _{ add ~ ws* ~ si ~ ws* ~ comma ~ ws* ~ imm8 }

cmp = { "CMP" }
cmp_exp = _{ cmp ~ ws* ~ al ~ ws* ~ comma ~ ws* ~ imm8 } 

int = { "INT" }
int_exp = _{ int ~ ws* ~ imm8 }

hlt = { "HLT" }

mov = { "MOV" }

mov_exp = _{ mov ~ ws* ~ (reg8  ~ ws* ~ comma ~ ws* ~ imm8
                        | reg16 ~ ws* ~ comma ~ ws* ~ imm16 
                        | reg32 ~ ws* ~ comma ~ ws* ~ imm32
                        | ss    ~ ws* ~ comma ~ ws* ~ ax 
                        | ds    ~ ws* ~ comma ~ ws* ~ ax 
                        | si    ~ ws* ~ comma ~ ws* ~ label
                        | al    ~ ws* ~ comma ~ ws* ~ lbranket ~ si ~ rbranket
                        | es    ~ ws* ~ comma ~ ws* ~ ax) }



exp = _{ ws* ~ 
    (db_exp | dw_exp | dd_exp | resb_exp | org_exp | jmp_exp | je_exp | dist_label 
     | int_exp | add_exp | cmp_exp | hlt | mov_exp )
        ~ ws* | empty }

        

